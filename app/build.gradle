apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'com.mostafa.bddexample'
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdk"]
    defaultConfig {
        applicationId "com.mostafa.bddexample"
        minSdkVersion globalConfiguration["androidMinSdk"]
        targetSdkVersion globalConfiguration["androidTargetSdk"]
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner getInstrumentation()
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'TEST_TAGS', '"' + getTestTags() + '"'
            buildConfigField 'String', 'TEST_SCENARIO', '"' + getTestScenario() + '"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    defaultConfig {
        testInstrumentationRunner getInstrumentation()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/com.android.tools/proguard/coroutines.pro']
        }
    }
    buildFeatures{
        dataBinding true
    }
}

def getInstrumentation() {
    project.hasProperty('cucumber') ? 'com.mostafa.bddexample.cucumber.runner.CucumberTestRunner' : 'androidx.test.runner.AndroidJUnitRunner'
}

def getTestTags() {
    project.getProperties().get('tags') ?: ''
}

def getTestScenario() {
    project.getProperties().get('scenario') ?: ''
}

dependencies {
    def ext = rootProject.extensions.getByName("ext")
    def deps = ext["supportDependencies"]
    implementation deps["coreKtx"]
    implementation deps["appCompat"]
    implementation deps["material"]
    implementation deps["constraintLayout"]
    testImplementation deps["junit"]
    androidTestImplementation deps["androidxJunit"]
    androidTestImplementation deps["espresso"]
    androidTestImplementation deps["espresso_intents"]
    androidTestImplementation deps["cucumber_android"]
    androidTestImplementation deps["cucumber_picocontainer"]
}